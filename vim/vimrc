"   ___      ___  __     ___      ___   _______    ______
"  |"  \    /"  ||" \   |"  \    /"  | /"      \  /" _  "\
"   \   \  //  / ||  |   \   \  //   ||:        |(: ( \___)
"    \\  \/. ./  |:  |   /\\  \/.    ||_____/   ) \/ \
"     \.    //   |.  |  |: \.        | //      /  //  \ _
"      \\   /    /\  |\ |.  \    /:  ||:  __   \ (:   _) \
"       \__/    (__\_|_)|___|\__/|___||__|  \___) \_______)
"

" {{{1 General

set nocompatible
set encoding=utf-8 " Use UTF-8 for encoding

set viminfo+=n$XDG_CACHE_HOME/vim/viminfo

syntax on
filetype plugin indent on

let mapleader = " " " Define <Leader> key

" {{{1 Plugins

" Load vim-plug
if empty(glob("~/.vim/autoload/plug.vim"))
    execute '!curl -fLo ~/.vim/autoload/plug.vim
    https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim'
endif

call plug#begin('~/.vim/bundle')

Plug 'baskerville/vim-sxhkdrc', { 'for': 'sxhkdrc' }
Plug 'bling/vim-airline'
Plug 'christoomey/vim-tmux-navigator'
Plug 'honza/vim-snippets'
Plug 'inside/vim-search-pulse'
Plug 'nosami/omnisharp', { 'for': 'cs', 'do': 'cd server && xbuild' }
Plug 'Raimondi/delimitMate'
Plug 'scrooloose/syntastic'
Plug 'Shougo/unite-outline'
Plug 'Shougo/unite.vim'
Plug 'Shougo/vimproc.vim', { 'do': 'make -f make_unix.mak' }
Plug 'SirVer/ultisnips'
Plug 'svermeulen/vim-easyclip'
Plug 'tommcdo/vim-exchange'
Plug 'tomtom/tcomment_vim'
Plug 'tpope/vim-dispatch'
Plug 'tpope/vim-fugitive'
Plug 'tpope/vim-repeat'
Plug 'tpope/vim-surround'
Plug 'tpope/vim-unimpaired'
Plug 'Valloric/YouCompleteMe', { 'do': './install.sh --clang-completer --system-libclang --omnisharp-completer' }
Plug 'wellle/targets.vim'
Plug 'whatyouhide/vim-gotham'

call plug#end()

" {{{2 Plugin configs

" {{{3 Airline

let g:airline_left_sep=''
let g:airline_right_sep=''

" {{{3 DelimitMate

" Turn on <Space> and <CR> expansion
let delimitMate_expand_cr = 2
let delimitMate_expand_space = 1

" Try to balance the closing pairs in the current line
let delimitMate_balance_matchpairs = 1

let delimitMate_nesting_quotes = ['"','`']

" {{{3 Easyclip

" Use 's' for substitution
let g:EasyClipUseSubstituteDefaults = 1

" Use 'x' as cut operator
let g:EasyClipUseCutDefaults = 0
nmap x          <Plug>MoveMotionPlug
nmap X          <Plug>MoveMotionEndOfLinePlug
nmap xX         <Plug>MoveMotionReplaceLinePlug
nmap xx         <Plug>MoveMotionLinePlug
xmap x          <Plug>MoveMotionXPlug

" {{{3 Fugitive

" Clean fugitive buffers
augroup fugitive
  autocmd!
  autocmd BufReadPost fugitive://* set bufhidden=delete
augroup END

" {{{3 UltiSnips

let g:UltiSnipsExpandTrigger="<c-j>"
let g:UltiSnipsJumpForwardTrigger="<c-j>"
let g:UltiSnipsJumpBackwardTrigger="<c-k>"
let g:UltiSnipsEditSplit="vertical"

" {{{3 Unite

nnoremap [unite] <NOP>
nmap <Space> [unite]

call unite#filters#matcher_default#use(['matcher_fuzzy'])
call unite#filters#sorter_default#use(['sorter_rank'])
let g:unite_source_rec_async_command =
            \ 'ag --follow --nocolor --nogroup --hidden -g ""'

nnoremap [unite]f :Unite -start-insert file_rec/async:!<CR>
nnoremap [unite]b :Unite buffer<CR>
nnoremap [unite]o :Unite -vertical -winwidth=50 -auto-highlight outline<CR>

" {{{3 Search pulse

let g:vim_search_pulse_mode = 'pattern'

" {{{3 Syntastic

let g:syntastic_cpp_compiler = 'clang++'
let g:syntastic_cpp_compiler_options = '-std=c++11 -stdlib=libc++'

" {{{3 YouCompleteMe

let g:ycm_autoclose_preview_window_after_completion = 1
let g:ycm_global_ycm_extra_conf = '~/src/cpp/.ycm_extra_conf.py'

" {{{1 Theme

set background=dark
colorscheme gotham

" {{{1 Setttings

" {{{2 Bakcup, swap and undo files

set nobackup
set nowritebackup
set noswapfile
set undodir=$XDG_CACHE_HOME/vim/undofiles
set undofile

" {{{2 Search options

set hlsearch                        " Highlight search terms
set incsearch                       " Show search matches as I type
set ignorecase                      " Ignore case when searching...
set smartcase                       " unless I use uppercase characters

" {{{2 Command, status line

set wildmenu                        " Use wildmenu for autocompletion
set noshowmode                      " Don't show current mode
set laststatus=2                    " Always show statusline
set showcmd                         " See keystrokes

" {{{2 Tabs and indenting

set shiftwidth=4                    " Number of spaces to use for indenting
set softtabstop=4                   " Number of spaces that a <Tab> counts for
set tabstop=4                       " How much space takes tab
set expandtab                       " Convert tabs into spaces
set smartindent                     " Do smart autoindenting
set cindent
set autoindent

set cino=g2,i0

" {{{2 Timeout

set timeout timeoutlen=1000
set ttimeout ttimeoutlen=100

" {{{2 Other

set autochdir                       " Automatically change working directory
set autoread                        " Read file if it was modified outside
set backspace=2                     " Make <Backspace> work correctly
set clipboard=unnamedplus
set exrc
set fillchars=vert:│
set hidden                          " Allow hidden buffers
set list                            " Show list chars
set listchars=tab:▸\ ,trail:⋅
let &showbreak='↪ '
set nowrap                          " Don't wrap lines
set scrolloff=10                    " Offset with the cursor when moving vertically
set sidescrolloff=15
set spell
set synmaxcol=300                   " Limit syntax highlight of long lines
set t_vb=
set visualbell                      " Don't beep and don't flash screen

" Show color column ONLY when necessary
call matchadd('ColorColumn', '\(\%81v\|\%100v\)', 100)

" {{{1 Mappings

" Surround character with spaces
nnoremap y<Space> a<Space><Esc>hi<Space><Esc>l

" Better navigation with wrapped lines
nnoremap j gj
nnoremap k gk

" Remove trailing whitespaces
nnoremap <F3> :call Preserve("%s/\\s\\+$//e")<CR>

" Open vimrc
nnoremap <Leader>v :edit $MYVIMRC<CR>

" Source vimrc
nnoremap <Leader>s :source $MYVIMRC<CR>

" Toggle fold
nnoremap <Space><Space> za

" Toggle `set list`
nnoremap <Leader>l :set list!<CR>

" Turn off highlighting for current search
nnoremap // :nohlsearch<CR>

" Faster saving
nnoremap <Leader>w :w<CR>

" Execute q recording
nnoremap Q @q

" Redo
nnoremap U <C-r>

" {{{1 Functions

" {{{2 Open files with ranger

function! RangerChooser()
  let temp = tempname()

  exec 'silent !ranger --choosefiles=' . shellescape(temp)

  if filereadable(temp)
    let names = readfile(temp)
    call delete(temp)

    if empty(names)
      return
    endif

    exec 'edit ' . fnameescape(names[0])

    for name in names[1:]
      exec 'argadd ' . fnameescape(name)
    endfor
  endif

  redraw!
endfunction

command! -bar RangerChooser call RangerChooser()
nnoremap <Leader>r :<C-U>RangerChooser<CR>

" {{{2 Return to last cursor position

function! s:SetCursorPosition()
  if &filetype !~ 'svn\|commit\c'
    if line("'\"") > 0 && line("'\"") <= line("$")
      exe "normal! g`\""
      normal! zz
    endif
  endif
endfunction

" {{{2 Save current position and execute command

function! Preserve(command)
  " Save last search, and cursor position.
  let _s=@/
  let l = line(".")
  let c = col(".")

  " Execute command
  execute a:command

  " Restore previous search history, and cursor position
  let @/=_s
  call cursor(l, c)
endfunction

" {{{1 Autocommands

augroup Autocommands
  autocmd!
  " Spell check when writing commit logs
  autocmd FileType    svn,*commit* setlocal spell

  " Return to last cursor position
  autocmd BufReadPost * call <SID>SetCursorPosition()

  autocmd FileType    markdown,text setlocal wrap nolist linebreak
augroup END

" }}}

" vim: fdm=marker
