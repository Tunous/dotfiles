"   ___      ___  __     ___      ___   _______    ______
"  |"  \    /"  ||" \   |"  \    /"  | /"      \  /" _  "\
"   \   \  //  / ||  |   \   \  //   ||:        |(: ( \___)
"    \\  \/. ./  |:  |   /\\  \/.    ||_____/   ) \/ \
"     \.    //   |.  |  |: \.        | //      /  //  \ _
"      \\   /    /\  |\ |.  \    /:  ||:  __   \ (:   _) \
"       \__/    (__\_|_)|___|\__/|___||__|  \___) \_______)
"
" Main settings -----------------------------------------------------------
" Initial settings {{{

set nocompatible
set encoding=utf-8

language messages en_US.utf-8

filetype plugin indent on
syntax on

" }}}
" Theme settings {{{

set t_Co=256                  " Use 256 colors
set background=dark           " Set dark background
colorscheme customized        " Use my own theme

" }}}
" Leader {{{

" Use ',' as leader
let mapleader=","

" }}}

" Plugins -----------------------------------------------------------------
" Vundle {{{

" Set paths {{{

set rtp+=$HOME/.vim/bundle/vundle
call vundle#rc('$HOME/.vim/bundle/')

" }}}

Bundle 'gmarik/vundle'

" }}}

" Airline {{{

" Nice looking statusline
Bundle 'bling/vim-airline'

" Use powerline symbols
let g:airline_powerline_fonts=1
let g:airline_detect_iminsert=0

" Show buffers line when there is only one tab open
let g:airline#extensions#tabline#enabled = 1

" }}}
" DelimitMate {{{

" Automatically close quotes, brackets, etc.
Bundle 'Raimondi/delimitMate'

let delimitMate_expand_cr = 1
let delimitMate_expand_space = 1
let delimitMate_balance_matchpairs = 1

" }}}
" Fugitive {{{

" Use git inside of vim
Bundle 'tpope/vim-fugitive'

" Auto-clean fugitive buffers {{{

augroup fugitive
  autocmd!
  autocmd BufReadPost fugitive://* set bufhidden=delete
augroup END

" }}}

" }}}
" Gitv {{{

Bundle 'gregsexton/gitv'

let g:Gitv_DoNotMapCtrlKey = 1

" }}}
" NERDTree {{{

Bundle 'scrooloose/nerdtree'

let NERDTreeDirArrows=1

" Close vim if only NERDTree is open {{{

augroup nerdTree
  autocmd!
  autocmd bufenter * if (winnr("$") == 1 && exists("b:NERDTreeType") && b:NERDTreeType == "primary") | q | endif
augroup END

" }}}

nnoremap <Leader>t :NERDTreeToggle<CR>

" }}}
" Surround {{{

Bundle 'tpope/vim-surround'

" }}}
" Sxhkdrc {{{

Bundle 'baskerville/vim-sxhkdrc'

" }}}
" Tabular {{{

" Line up text
Bundle 'godlygeek/tabular'

" }}}
" Tcomment {{{

" Comment selected text
Bundle 'tomtom/tcomment_vim'

" }}}

" Actual settings ---------------------------------------------------------
" Settting options {{{

" Bakcup and swap files {{{

" Don't create swap and backup files
set nobackup
set nowritebackup
set noswapfile

" }}}
" Mouse {{{

" Can use mouse in all modes
if has("mouse")
  set mouse=a
endif

" }}}
" Search options {{{

set hlsearch                      " Highlight search terms
set incsearch                     " Show search matches as I type
set ignorecase                    " Ignore case when searching...
set smartcase                     " unless I use uppercase characters

" }}}
" Smart scrolling {{{

set scrolloff=10
set sidescrolloff=15

" }}}
" Tabs and spaces {{{

set tabstop=4                     " Number of spaces for tab
set shiftwidth=2                  " Number of spaces to use for indenting
set softtabstop=2                 " Number of spaces that tab counts for
set expandtab                     " Use spaces instead of tabs
set autoindent                    " Always set autoindenting on
set copyindent                    " Copy the previous indentation on autoindenting

" }}}
" Undo files {{{

let s:undofilesDir = "$HOME/.vim/undofiles"

if !isdirectory(s:undofilesDir)
  silent execute '!mkdir "' . s:undofilesDir . '" >/dev/null 2>&1'
endif

execute 'set undodir=' . s:undofilesDir
set undofile

" }}}
" Other {{{

" Add short timeout
set timeout
set timeoutlen=1000
set ttimeoutlen=100

set wildmenu                      " Use wildmenu for commandline autocompletion

set autoread                      " Read file if it was modified outside of vim
set autochdir                     " Automatically change working directory to directory of current file

set hidden                        " Allow hidden buffers

set showcmd                       " Show executed command in commands line

set cursorcolumn                  " Highlight cursor column...
set cursorline                    " ... and line

set backspace=indent,eol,start    " Make backspace delete everything in insert mode

set laststatus=2                  " Always show statusline

" Don't beep and don't flash screen
set visualbell
set t_vb=

set nowrap                        " Don't wrap lines

" Show list chars
set list
set listchars=tab:▸\ ,trail:·

set foldmethod=marker             " Use marker foldmethod

set synmaxcol=300                 " Limit syntax highlight of long lines

" }}}

" }}}
" Key mappings {{{

" Buffers navigation {{{

nnoremap <Leader>N :bp<CR>
nnoremap <Leader>n :bn<CR>
nnoremap <Leader>b :ls<CR>:b<Space>
nnoremap <Leader>d :bd<CR>

" }}}
" Nvigation {{{

" Better navigation with wrapped lines
nnoremap j gj
nnoremap k gk

" Change windows
noremap <C-k> <C-W>k
noremap <C-j> <C-W>j
noremap <C-h> <C-W>h
noremap <C-l> <C-W>l

" Don't use arrow keys
noremap <Up> <NOP>
noremap <Down> <NOP>
noremap <Left> <NOP>
noremap <Right> <NOP>
inoremap <Up> <NOP>
inoremap <Down> <NOP>
inoremap <Left> <NOP>
inoremap <Right> <NOP>

" }}}
" Open config files {{{

" Open vimrc
nnoremap <Leader>vv :edit $HOME/.vim/vimrc<CR>

" Open vimperatorrc
nnoremap <Leader>vp :edit $HOME/.vimperator/vimperatorrc<CR>

" Open customized colorscheme
nnoremap <Leader>vt :edit $HOME/.vim/colors/customized.vim<CR>

" }}}
" Other {{{

" Capitalize word next to cursor
inoremap <C-u> <Esc>:call Preserve("normal gUiw")<CR>a

" Indend whole file and remove trailing whitespaces
nnoremap <F2> :call Preserve("normal gg=G")<CR>
nnoremap <F3> :call Preserve("%s/\\s\\+$//e")<CR>

" Save file
nnoremap <Enter> :w<CR>

" Source vimrc
nnoremap <Leader>s :source $MYVIMRC<CR>zv

" Toggle fold
nnoremap <Space> za

" Toggle `set list`
nmap <Leader>l :set list!<CR>

" Turn off highlighting for current search
nnoremap <C-n> :nohlsearch<CR>

" Yank to end of line
nnoremap Y y$

" }}}

" }}}
" Functions {{{

" Insert header preprocessor gates {{{

function! s:InsertCppGates()
  let gatename = substitute(toupper(expand("%:t")), "\\.", "_", "g")
  execute "normal! i#ifndef " .gatename
  execute "normal! o#define " .gatename
  execute "normal! Go#endif"
  normal! kk
endfunction

" }}}
" Return to last cursor position {{{

function! s:SetCursorPosition()
  if &filetype !~ 'svn\|commit\c'
    if line("'\"") > 0 && line("'\"") <= line("$")
      exe "normal! g`\""
      normal! zz
    endif
  end
endfunction

" }}}
" Save current position and execute command {{{

function! Preserve(command)
  " Save last search, and cursor position.
  let _s=@/
  let l = line(".")
  let c = col(".")

  " Execute command
  execute a:command

  " Restore previous search history, and cursor position
  let @/=_s
  call cursor(l, c)
endfunction

" }}}
" Toggle number column {{{

let s:NumberExclude = [ '', 'nerdtree', 'help', 'gitv' ]

function! NumberToggle(what)
  if index(s:NumberExclude, &ft) >= 0
    " Do nothing

  elseif a:what == 'on'
    " Turn on relative numbers
    setlocal number
    setlocal relativenumber

  elseif a:what == 'off'
    " Turn off numbers
    setlocal nonumber
    setlocal norelativenumber

  elseif a:what == 'norel'
    " Turn on normal numbers
    setlocal number
    setlocal norelativenumber
  endif
endfunction

" }}}

" }}}
" Autocommands {{{

" Relative numbers {{{

augroup relnum
  au!
  au BufEnter    * call NumberToggle('on')
  au BufLeave    * call NumberToggle('off')

  au InsertEnter * call NumberToggle('norel')
  au InsertLeave * call NumberToggle('on')

  au FocusLost   * call NumberToggle('norel')
  au FocusGained * call NumberToggle('on')
augroup END

" }}}
" Other {{{

augroup other
  autocmd!
  " Add header file preprocessor getes
  autocmd BufNewFile,BufRead *.{h,hpp} call <SID>InsertCppGates()

  " Spell check when writing commit logs
  autocmd filetype svn,*commit* setlocal spell

  " Return to last cursor position
  autocmd BufReadPost * call <SID>SetCursorPosition()
augroup END

" }}}

" }}}

" End {{{
" vim: foldmethod=marker: foldlevel=0:
