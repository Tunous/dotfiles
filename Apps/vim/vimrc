"   ___      ___  __     ___      ___   _______    ______
"  |"  \    /"  ||" \   |"  \    /"  | /"      \  /" _  "\
"   \   \  //  / ||  |   \   \  //   ||:        |(: ( \___)
"    \\  \/. ./  |:  |   /\\  \/.    ||_____/   ) \/ \
"     \.    //   |.  |  |: \.        | //      /  //  \ _
"      \\   /    /\  |\ |.  \    /:  ||:  __   \ (:   _) \
"       \__/    (__\_|_)|___|\__/|___||__|  \___) \_______)
"

" {{{ General

set nocompatible
set encoding=utf-8 " Use UTF-8 for encoding

set viminfo+=n$XDG_CACHE_HOME/vim/viminfo

syntax on
filetype off

let mapleader = "," " Define <Leader> key

" }}}
" {{{ Plugins
set rtp+=~/.vim/bundle/Vundle.vim
call vundle#begin()

Plugin 'gmarik/Vundle.vim'
Plugin 'bling/vim-airline'
Plugin 'bling/vim-bufferline'
Plugin 'a.vim'
Plugin 'Raimondi/delimitMate'
Plugin 'junegunn/vim-easy-align'
Plugin 'svermeulen/vim-easyclip'
Plugin 'tpope/vim-eunuch'
Plugin 'tpope/vim-fugitive'
Plugin 'gregsexton/gitv'
Plugin 'tpope/vim-repeat'
Plugin 'justinmk/vim-sneak'
Plugin 'tpope/vim-surround'
Plugin 'baskerville/vim-sxhkdrc'
Plugin 'scrooloose/syntastic'
Plugin 'tomtom/tcomment_vim'
Plugin 'SirVer/ultisnips'
Plugin 'honza/vim-snippets'
Plugin 'tpope/vim-unimpaired'
Plugin 'Valloric/YouCompleteMe'
Plugin 'tommcdo/vim-exchange'
Plugin 'groenewege/vim-less'
Plugin 'Tunous/vim-colors-customized'

call vundle#end()
filetype plugin indent on

" {{{ Plugin configs

" {{{ Airline
" -------------------------------------------------------------------------
" Use powerline symbols
let g:airline_powerline_fonts=1

" }}}
" {{{ Bufferline
" -------------------------------------------------------------------------
" Don't echo to the command bar
" Airline is used to display open buffers
let g:bufferline_echo = 0

" }}}
" {{{ DelimitMate
" -------------------------------------------------------------------------
" Turn on <Space> and <CR> expansion
let delimitMate_expand_cr = 1
let delimitMate_expand_space = 1

" Try to balance the closing pairs in the current line
let delimitMate_balance_matchpairs = 1

" }}}
" {{{ Easy align
" -------------------------------------------------------------------------
vmap <CR>         <Plug>(EasyAlign)
nmap <Leader>a    <Plug>(EasyAlign)

" }}}
" {{{ Easyclip
" -------------------------------------------------------------------------
" Use 'gs' for substitution
nmap gs         <Plug>SubstituteOverMotionMap
nmap gss        <Plug>SubstituteLine
xmap gs         <Plug>XEasyClipPaste

" Use 'x' as cut operator
let g:EasyClipUseCutDefaults = 0
nmap x          <Plug>MoveMotionPlug
nmap X          <Plug>MoveMotionEndOfLinePlug
nmap xX         <Plug>MoveMotionReplaceLinePlug
nmap xx         <Plug>MoveMotionLinePlug
xmap x          <Plug>MoveMotionXPlug

" }}}
" {{{ Fugitive
" -------------------------------------------------------------------------
" Clean fugitive buffers
augroup fugitive
  autocmd!
  autocmd BufReadPost fugitive://* set bufhidden=delete
augroup END

" }}}
" {{{ Gitv
" -------------------------------------------------------------------------
" Don't map ctrl key - it conflicts with my navigation mappings
let g:Gitv_DoNotMapCtrlKey = 1

" }}}
" {{{ Sneak
" -------------------------------------------------------------------------
" Use s as sneak operator
let g:sneak#use_ic_scs = 1
nmap s <Plug>SneakForward
nmap S <Plug>SneakBackward
xmap s <Plug>VSneakForward
xmap S <Plug>VSneakBackward

" }}}
" {{{ UltiSnips 
" -------------------------------------------------------------------------
let g:UltiSnipsExpandTrigger="<c-j>"
let g:UltiSnipsEditSplit="vertical"

" }}} 
" {{{ YouCompleteMe 
" -------------------------------------------------------------------------
let g:ycm_autoclose_preview_window_after_completion = 1

" }}} 

" }}}
" }}}
" {{{ Theme

set background=dark
let g:customized_use_xresources = 1
colorscheme customized

" }}}
" {{{ Setttings

" {{{ Bakcup, swap and undo files

set nobackup
set nowritebackup
set noswapfile
set undodir=$XDG_CACHE_HOME/vim/undofiles
set undofile

" }}}
" {{{ Search options

set hlsearch                        " Highlight search terms
set incsearch                       " Show search matches as I type
set ignorecase                      " Ignore case when searching...
set smartcase                       " unless I use uppercase characters

" }}}
" {{{ Command, status line

set wildmenu                        " Use wildmenu for autocompletion
set noshowmode                      " Don't show current mode
set laststatus=2                    " Always show statusline
set showcmd                         " See keystrokes

" }}}
" {{{ Tabs and indenting

set shiftwidth=4                    " Number of spaces to use for indenting
set softtabstop=4                   " Number of spaces that a <Tab> counts for
set expandtab                       " Convert tabs into spaces
set smartindent                     " Do smart autoindenting
set copyindent                      " Copy the previous indentation characters

" }}}
" {{{ Timeout

set timeout timeoutlen=1000
set ttimeout ttimeoutlen=100

" }}}
" {{{ Other

set autoread                        " Read file if it was modified outside
set autochdir                       " Automatically change working directory
set hidden                          " Allow hidden buffers
set backspace=2                     " Make <Backspace> work correctly
set visualbell                      " Don't beep and don't flash screen
set t_vb=
set nowrap                          " Don't wrap lines
set list                            " Show list chars
set listchars=tab:▸\ ,trail:⋅
set foldmethod=syntax               " Use syntax foldmethod
set synmaxcol=300                   " Limit syntax highlight of long lines
set scrolloff=10                    " Offset with the cursor when moving vertically
set sidescrolloff=15

call matchadd('ColorColumn', '\%81v', 100) " Show column 80 ONLY when necessary

" }}}

" }}}
" {{{ Mappings

" {{{ Nvigation/Resizing

" Better navigation with wrapped lines
nnoremap j gj
nnoremap k gk

" Change windows
nnoremap <C-h> <C-W>h
nnoremap <C-j> <C-W>j
nnoremap <C-k> <C-W>k
nnoremap <C-l> <C-W>l

" Resize splits
nnoremap <Esc>h <C-W><
nnoremap <Esc>j <C-W>-
nnoremap <Esc>k <C-W>+
nnoremap <Esc>l <C-W>>

" Delete focused buffer
nnoremap <Leader>d :bd<CR>

" }}}
" {{{ Other

" Capitalize word next to cursor
inoremap <C-u> <Esc>:call Preserve("normal gUiw")<CR>a

" Remove trailing whitespaces
nnoremap <F3> :call Preserve("%s/\\s\\+$//e")<CR>

" Open vimrc
nnoremap <Leader>v :edit $MYVIMRC<CR>

" Source vimrc
nnoremap <Leader>s :source $MYVIMRC<CR>

" Toggle fold
nnoremap <Space> za

" Toggle `set list`
nmap <Leader>l :set list!<CR>

" Turn off highlighting for current search
nnoremap <Leader>h :nohlsearch<CR>

" }}}

" }}}
" {{{ Functions

" {{{ Open files with ranger

function! RangerChooser()
  let temp = tempname()

  exec 'silent !ranger --choosefiles=' . shellescape(temp)

  if filereadable(temp)
    let names = readfile(temp)
    call delete(temp)

    if empty(names)
      return
    endif

    exec 'edit ' . fnameescape(names[0])

    for name in names[1:]
      exec 'argadd ' . fnameescape(name)
    endfor
  endif

  redraw!
endfunction

command! -bar RangerChooser call RangerChooser()
nnoremap <Leader>r :<C-U>RangerChooser<CR>

" }}}
" {{{ Return to last cursor position

function! s:SetCursorPosition()
  if &filetype !~ 'svn\|commit\c'
    if line("'\"") > 0 && line("'\"") <= line("$")
      exe "normal! g`\""
      normal! zz
    endif
  endif
endfunction

" }}}
" {{{ Save current position and execute command

function! Preserve(command)
  " Save last search, and cursor position.
  let _s=@/
  let l = line(".")
  let c = col(".")

  " Execute command
  execute a:command

  " Restore previous search history, and cursor position
  let @/=_s
  call cursor(l, c)
endfunction

" }}}

" }}}
" {{{ Autocommands

augroup Autocommands
  autocmd!
  " Spell check when writing commit logs
  autocmd filetype    svn,*commit* setlocal spell

  " Return to last cursor position
  autocmd BufReadPost * call <SID>SetCursorPosition()

  autocmd FileType    markdown setlocal wrap nolist linebreak
augroup END

" }}}

" vim: fdm=marker
